allprojects {

	apply plugin: 'java'	
	apply plugin: 'groovy'

	version = '1.0'

	repositories {
		mavenLocal()
		mavenCentral()
	}

	dependencies {				

		//logging
		compile 'org.slf4j:slf4j-api:1.7.5'
		compile 'org.slf4j:slf4j-simple:1.7.5'

		//dependency injection
		compile 'com.google.inject:guice:3.0'	
		
		compile 'com.google.guava:guava:15.0'
		
		//spock & related		
		testCompile 'org.codehaus.groovy:groovy-all:2.0.7'
		testCompile 'org.spockframework:spock-guice:0.7-groovy-2.0'
		testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
	}
}

def groovyProjects() {
	//assume service and persistence projects do not use Groovy in code other
	//than the Spock tests.
	subprojects.findAll {		
		!it.name.contains('service')		
	}
}

def persistenceProjects() {
	subprojects.findAll() {
		it.name.contains('persistence')
	}
}

def persistenceUnitProjects() {
	subprojects.findAll() {
		it.name.contains('persistence-')
	}
}

configure(groovyProjects()) {
	apply plugin: 'groovy'
	dependencies {
		compile 'org.codehaus.groovy:groovy-all:2.0.5'
	}
}

configure(persistenceUnitProjects()) {
	dependencies {
		compile project(':carplots-persistence')
	}
}

configure(persistenceProjects()) {
	dependencies {
		compile 'org.hibernate:hibernate-entitymanager:4.2.3.Final'
		compile 'org.hibernate:hibernate-c3p0:4.2.3.Final'
		compile 'mysql:mysql-connector-java:5.1.25'
		compile 'com.google.inject.extensions:guice-persist:3.0'
		compile 'org.springframework:spring-orm:3.2.3.RELEASE'
		compile 'org.jodah:typetools:0.3.0'
	}
}

configure(subprojects.findAll {it.name != 'carplots-common'}) {
	//common project libraries
	dependencies {
		 compile project(':carplots-common')
	}
}

task wrapper(type: Wrapper) {
	gradleVersion = '1.5'
}
